apply plugin: 'com.android.application'

android {
    signingConfigs {
        config {
            keyAlias 'keyapps'
            keyPassword 'admin1010'
            storeFile file('C:/Users/r028367/Documents/keyapp/keyapp.jks')
            storePassword '@admin1010'
        }
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "xplore.com.br.customviewpart2"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 2
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    flavorDimensions "d3"
    productFlavors {
        fullVersion {
        }
        demo {
            applicationIdSuffix = ".demo"
        }
        custom {
            applicationId "xplore.com.br.custom.customviewpart2"
            versionCode 1
            versionName "1.0.0"
        }
        christmas {
            applicationId "xplore.com.br.natalina.customviewpart2"
            version 1
            versionName "1.0.0"
            signingConfig signingConfigs.config
            targetSdkVersion 27
        }
    }
    /**
     * atributos de um tipo de compicacao
     *
     * debuggable: permite que o aplicativo seja depurado num dispositivo (virtual ou fisico)
     * apps que vao para google play nao podem ser 'depuraveis'
     *
     * applicationIdSuffix e versionNameSuffix permite criar no mesmo app N-versoes de compilacao
     * diferentes e combinar com os tipos de produtos (productFlavors)
     *
     * BuildConfigField: com esse atributo podemos criar atributos para a classe BuildConfig gerada
     * pelo gradle
     *
     **/
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        dev {
            debuggable true
            applicationIdSuffix ".dev"
            versionNameSuffix "-DEV"
            buildConfigField "boolean", "REPORT_CRASH", "false"
        }

        homolog {
            debuggable true
            applicationIdSuffix ".homolog"
            versionNameSuffix "-HOMOLOG"
            buildConfigField "boolean", "REPORT_CRASH", "true"
            buildConfigField "String", "ENDPOINT", "\"http://192.168.0.1/admin\""
            minifyEnabled true
        }

        production {
            initWith release
            applicationIdSuffix ".production"
            buildConfigField "boolean", "REPORT_CRASH", "true"
        }
    }
    sourceSets {
        demo { res.srcDirs = ['src/demo/res', 'src/demo/res/'] }
        fullVersion { res.srcDirs = ['src/fullVersion/res', 'src/fullVersion/res/'] }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
